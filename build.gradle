import org.gradle.internal.os.OperatingSystem

plugins {
  id "com.ullink.msbuild" version "2.8"
  id "com.ullink.nuget" version "2.7"
  id "com.ullink.nunit" version "1.2"
  id "com.ullink.opencover" version "1.1"
  id "com.ullink.functions" version "1.0"
}

archivesBaseName = 'ILRepack'
version = '2.0.2'

defaultTasks('nugetPack')

def removeExtension(String path) {
    path.replaceFirst(~/(?<=.)\.[^\.]+$/, '')
}

msbuild.dependsOn nugetRestore
msbuild {
    def netVersion = project.version + '.0'
    replaceAssemblyAttribute('ILRepack/Properties/AssemblyInfo.cs', 'AssemblyVersion', netVersion)
    replaceAssemblyAttribute('ILRepack/Properties/AssemblyInfo.cs', 'AssemblyFileVersion', netVersion)

    solutionFile = 'ILRepack.sln'
    configuration = OperatingSystem.current().unix ? 'Debug_unix' : 'Release'
    projectName = 'ILRepack'
    inputs.file(project.buildFile)
}

nunit {
    exclude 'LongRunning'
    testAssemblies = [ msbuild.projects['ILRepack.Tests'].properties.TargetPath, msbuild.projects['ILRepack.IntegrationTests'].properties.TargetPath ]
}
nunit.dependsOn msbuild

task nunitLongRunning(type: nunit.class) {
    include 'LongRunning'
    testAssemblies = [ msbuild.projects['ILRepack.IntegrationTests'].properties.TargetPath ]
}
nunitLongRunning.dependsOn msbuild

ext.repackList = ['ILRepack.exe', 'Fasterflect.dll', 'BamlParser.dll', 'Mono.Posix.dll']

// repacking of all dependencies in a single exe
task repack(dependsOn: [msbuild, nunitLongRunning]) {
    inputs.files {
        def workingDir = msbuild.mainProject.getProjectPropertyPath('OutputPath')
        return project.ext.repackList.collect { new File(workingDir, it) }
    }
    ext.repacked = new File(temporaryDir, 'ILRepack.exe')
    ext.repackedLib = new File(temporaryDir, 'ILRepack.dll')
    outputs.files([ext.repacked, ext.repackedLib, removeExtension(ext.repacked.path)+'.pdb'])
}

repack << {
    exec {
        workingDir = msbuild.mainProject.getProjectPropertyPath('OutputPath')
        commandLine = [new File(workingDir, 'ILRepack.exe'), '/log', '/wildcards', '/internalize', '/out:'+ext.repacked] + project.ext.repackList
    }
    exec {
        workingDir = msbuild.mainProject.getProjectPropertyPath('OutputPath')
        commandLine = [new File(workingDir, 'ILRepack.exe'), '/log', '/wildcards', '/internalize', '/ndebug', '/keyfile:'+file('ILRepack/ILRepack.snk'), '/out:'+ext.repackedLib, '/target:library'] + project.ext.repackList
    }
}

def commonNuspecMetadata(def meta) {
    meta.version version
    meta.title 'ILRepack - Open-source alternative to ILMerge'
    meta.authors 'Francois Valdy'
    meta.owners 'Francois Valdy'
    meta.projectUrl('https://github.com/gluck/il-repack')
    meta.copyright 'Copyright © Francois Valdy 2011-2015'
}

// nuget package for upload to nuget
nugetPack {
    nuspec {
        metadata() {
            commonNuspecMetadata(delegate)
            id archivesBaseName
            delegate.description '''ILRepack is meant at replacing ILMerge / Mono.Merge.
            The former being closed-source, impossible to customize, slow, resource consuming and many more. The later being deprecated, unsupported, and based on an old version of Mono.Cecil.'''
            requireLicenseAcceptance false
            summary 'ILRepack is a utility that can be used to merge multiple .NET assemblies into a single assembly'
        }
        delegate.files() {
            delegate.file(src: repack.ext.repacked, target: 'tools')
        }
    }
}

task nugetPackLib(type: com.ullink.NuGetPack) {
    nuspec {
        metadata() {
            commonNuspecMetadata(delegate)
            id archivesBaseName+'.Lib'
            delegate.description '''ILRepack is meant at replacing ILMerge / Mono.Merge.
            The former being closed-source, impossible to customize, slow, resource consuming and many more. The later being deprecated, unsupported, and based on an old version of Mono.Cecil.

            This package provides a library, for use within tools/build projects.
            If you need the tool as an executable, this is not the package you're looking for, this one is: http://www.nuget.org/packages/ILRepack/.'''
            requireLicenseAcceptance false
            summary 'ILRepack is a utility that can be used to merge multiple .NET assemblies into a single assembly (Packaged as library)'
        }
        delegate.files() {
            delegate.file(src: repack.ext.repackedLib, target: 'lib/net40')
        }
    }
}

nugetPack.dependsOn([repack, nugetPackLib])
nugetPackLib.dependsOn repack

// nuget package upload, requires API key to be set
nugetPush {
    apiKey = project.properties.nugetApiKey
    nupkgFile = nugetPack.packageFile
}

task nugetPushLib(type: com.ullink.NuGetPush) {
    apiKey = project.properties.nugetApiKey
    nupkgFile = nugetPackLib.packageFile
}

nugetPush.dependsOn nugetPushLib
